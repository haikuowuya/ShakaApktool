/**
 *  Copyright 2015 Rover12421 <rover12421@163.com>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

apply from  : 'gradle/functions.gradle'

ext.langLevel = '1.7'
ext.aspectjVersion = '1.8.4'
version = '2.0.0'

def branch = getCheckedOutBranchFromProject()
def hash = getCheckedOutGitCommitHashFromProject()
def date = new Date().format("yyyyMMdd")

ext.shakaVersion = "$version-$branch-$hash-$date"
ext.shakaDebug = false

buildscript {
    repositories {
        jcenter()
        maven {
            url "https://maven.eveoh.nl/content/repositories/releases"
        }
    }
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.incremental = true
    }
    dependencies {
        classpath 'eu.appsatori:gradle-fatjar-plugin:0.3'
        classpath 'nl.eveoh:gradle-aspectj:1.5'
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    repositories {
        mavenCentral()
        jcenter()
    }

    sourceCompatibility = langLevel
    targetCompatibility = langLevel

    task deleteProperties(type: Delete) {
        delete buildDir.getAbsolutePath() + '/resources/main'
    }

    processResources.dependsOn deleteProperties
}

subprojects {
    apply plugin: 'maven'
    apply plugin: 'aspectj'
    apply plugin: 'eu.appsatori.fatjar'

    ext {
        depends = [
                antlr           : 'org.antlr:antlr:3.5.2',
                antlr_runtime   : 'org.antlr:antlr-runtime:3.5.2',
                aspectj         : 'org.aspectj:aspectjrt:1.8.4',
                commons_cli     : 'commons-cli:commons-cli:1.2',
                commons_compress: 'org.apache.commons:commons-compress:1.4.1',
                commons_io      : 'commons-io:commons-io:2.4',
                commons_lang    : 'org.apache.commons:commons-lang3:3.1',
                dx              : 'com.google.android.tools:dx:1.7',
                findbugs        : 'com.google.code.findbugs:jsr305:1.3.9',
                gson            : 'com.google.code.gson:gson:2.2.4',
                guava           : 'com.google.guava:guava:18.0',
                jflex           : 'de.jflex:jflex:1.6.1',
                jflex_plugin    : 'co.tomlee.gradle.plugins:gradle-jflex-plugin:0.0.1',
                junit           : 'junit:junit:4.11',
                proguard        : 'net.sf.proguard:proguard-gradle:5.2.1',
                snakeyaml       : 'org.yaml:snakeyaml:1.12',
                stringtemplate  : 'org.antlr:stringtemplate:3.2.1',
                ST4             : 'org.antlr:ST4:4.0.7',
                xmlunit         : 'xmlunit:xmlunit:1.3',
                xmlpull         : 'xpp3:xpp3:1.1.4c',
        ]
    }

    dependencies {
        testCompile depends.junit
    }
}

// 过滤shaka.lib,其他项目均可以包含该项目
configure(subprojects - project(':shaka.lib')) {
    dependencies {
        compile project(':shaka.lib')
        // acj 的全局注入
        aspectpath files(project(':shaka.lib').buildDir)
    }
}

idea {
    project {
        languageLevel = langLevel
    }

    module {
        excludeDirs += files(
                'TestApk',
                'src/test/resources/temp'
        )
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

// used for official releases only. Please don't use
task release {
}

