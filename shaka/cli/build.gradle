/**
 *  Copyright 2015 Rover12421 <rover12421@163.com>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

apply plugin: 'eu.appsatori.fatjar'

configurations {
    proguard
}

dependencies {
    compile project(':shaka:apktool')

    proguard depends.proguard
}

gradle.taskGraph.whenReady {
    fatJar {
        manifest {
            attributes("Main-Class": "com.rover12421.shaka.cli.Main")
        }
    }
}

task proguard(type: JavaExec, dependsOn: fatJar) {
    def outFile = jar.destinationDir.getPath() + '/' + "ShakaApktool" + '_' + shakaVersion + '-release' + '.' + jar.extension
    inputs.file jar.archivePath
    outputs.file outFile

    def mappingFile = new File(new File(outFile).getParentFile(), "mapping.txt");

    classpath = configurations.proguard
    main = 'proguard.ProGuard'
    args '-injars ' + "${jar.archivePath}" + ' (!META-INF/LICENSE.txt,!META-INF/NOTICE.txt,!README)'
    args "-outjars ${outFile}"
    args "-libraryjars ${System.properties['java.home']}/lib/rt.jar"
    args "-printmapping ${mappingFile}"
//    args '-dontobfuscate'
    args '-dontoptimize'
    args '-dontnote **'
    args '-dontwarn **'
    args '-keep public class Main { public static void main(java.lang.String[]); }'
    args '-keep public class com.rover12421.shaka.cli.Main { public static void main(java.lang.String[]); }'
    args '-keep class org.yaml.snakeyaml.** { public protected private *; }'
    args '-keepclassmembers enum * { public static **[] values(); public static ** valueOf(java.lang.String); }'
    args '-keepclassmembers class brut.androlib.res.data.ResResSpec { *** mName; }'
    args '-keepclassmembers class brut.androlib.res.data.value.ResFileValue { *** mPath; }'
    args '-keepclassmembers class brut.androlib.meta.** { public <fields>; }'
}

tasks.getByPath(':release').dependsOn(proguard)
